#include "boot_info.ika"

// arch
#include "gdt.ika"
#include "idt.ika"
#include "isr.ika"
#include "irq.ika"
#include "interrupt.ika"
#include "pmm.ika"
#include "vmm.ika"

// drivers
#include "console.ika"
#include "timer.ika"
#include "kbd.ika"

// lib
#include "print.ika"
#include "getc.ika"
#include "time.ika"
#include "malloc.ika"

// fs
#include "vfs.ika"
#include "ext2.ika"
#include "initrd.ika"

fn color_test() void {
    "Color test:\n";
    "Standard: ";
    "\x1b[97;40m   0 \x1b[0m";
    "\x1b[97;41m   1 \x1b[0m";
    "\x1b[97;42m   2 \x1b[0m";
    "\x1b[97;43m   3 \x1b[0m";
    "\x1b[97;44m   4 \x1b[0m";
    "\x1b[97;45m   5 \x1b[0m";
    "\x1b[97;46m   6 \x1b[0m";
    "\x1b[97;47m   7 \x1b[0m";

    "\nIntense:  ";
    "\x1b[30;100m   8 \x1b[0m";
    "\x1b[30;101m   9 \x1b[0m";
    "\x1b[30;102m  10 \x1b[0m";
    "\x1b[30;103m  11 \x1b[0m";
    "\x1b[30;104m  12 \x1b[0m";
    "\x1b[30;105m  13 \x1b[0m";
    "\x1b[30;106m  14 \x1b[0m";
    "\x1b[30;107m  15 \x1b[0m";
    "\x1b[0m\n";
}

fn print_memory_map(info: *BootInfo) void {
    "Kernel end: 0x%x\n", info.kernel_end;
    "Memory map:\n";
    var i: u32 = 0;
    while (i < info.mmap_count) : (i += 1) {
        if (info.mmap[i].base.high == 0 && info.mmap[i].length.high == 0) {
            "mmap[%d]:\n", i;
            "    addr: 0x%x\n", info.mmap[i].base.low;
            "    length: %u\n", info.mmap[i].length.low;
        }
    }
}

fn heap_test() void {
    "Heap test:\n";
    "Allocating memory...\n";
    var a: *void = malloc(128);
    var b: *void = malloc(128);
    var c: *void = malloc(128);
    "a=%p, b=%p, c=%p\n", a, b, c;
    debug_malloc_print_free_list();

    "Freeing memory...\n";
    free(a);
    free(b);
    free(c);
    debug_malloc_print_free_list();
}

fn kbd_test() void {
    "Keyboard test: ";
    while (true) {
        var c: i32 = getchar();
        putchar(c);
        if (c == '\n') {
            break;
        }
    }
}

fn timer_test() void {
    "Timer test:\n";
    var i: u32 = 0;
    while (i < 5) {
        i += 1;
        sleep(1000);
        if (i == 1) {
            "%d second has passed\n", i;
        } else {
            "%d seconds have passed\n", i;
        }
    }
}

pub fn kmain(info: *BootInfo) void {
    // core modules
    gdt_init();
    idt_init();
    isr_init();
    irq_init();

    console_init();

    // hardware drivers
    timer_init(10000);
    kbd_init();

    // memory management
    pmm_init(info);
    vmm_init(info);

    // file system
    vfs_init();
    ext2_init();
    // initdr_mount(info);

    interrupt_enable();
    "takos is running! %s\n", "WAH!";

    print_memory_map(info);
    color_test();
    heap_test();
    kbd_test();
    timer_test();

    "Interrupt test:\n";
    breakpoint();
}
